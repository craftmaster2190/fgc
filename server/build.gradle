buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.hibernate:hibernate-gradle-plugin:6.0.0.Alpha4"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.27.2'
    id 'com.github.spotbugs' version '4.0.4'
    id 'eclipse'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'name.remal.check-dependency-updates' version '1.0.178'
    id 'org.flywaydb.flyway' version '6.3.1'
    id 'org.owasp.dependencycheck' version '5.3.1'
    id 'org.springframework.boot' version '2.2.5.RELEASE'
}

group = 'com.craftmaster.lds'
sourceCompatibility = '11'
version = '1.0'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    flyway {
        extendsFrom implementation
    }
    compile {
        exclude module: 'jackson-datatype-jsr310'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    flyway 'org.flywaydb:flyway-core:6.3.1'
    flyway 'org.postgresql:postgresql:42.2.11.jre7'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.1'
    implementation 'com.google.googlejavaformat:google-java-format:1.7'
    implementation 'com.impossibl.pgjdbc-ng:pgjdbc-ng:0.8.3'
    implementation 'io.sentry:sentry-spring:1.7.30'
    implementation 'org.owasp:dependency-check-gradle:5.3.1'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.session:spring-session-jdbc'
    runtimeOnly 'com.h2database:h2'
    // this seems to add more checks to spotbugs
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
// to postgre dump a schema
// pg_dump -O -T fgc.flyway_schema_history -U postgres fgc > V1__Base_version.sql
flyway {
    url = 'jdbc:postgresql://localhost:5432/fgc'
    user = 'postgres'
    password = 'postgres'
    schemas = ['public']
    locations = ['filesystem:src/flyway']
    configurations = ['flyway']
}

eclipse {
    project {
        // this is the name of the parent dir
        name = 'server'
    }
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

dependencyCheck {
    format = 'ALL'
}

apply plugin: 'org.hibernate.orm'
hibernate {
    enhance {
        enableLazyInitialization = true
        enableDirtyTracking = true
        enableAssociationManagement = true
        enableExtendedEnhancement = false
    }
}

spotbugs {
    ignoreFailures = true
    showProgress = true
    effort = 'max'
    reportLevel = 'high'
    reportsDir = file("$buildDir/spotbugs")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

